plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    google()
}

group 'ai.bitlabs'
version '2.2.9'

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 29
        versionName version
    }

    flavorDimensions 'sdk'

    productFlavors {
        core {}
        unity {}
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    unityCompileOnly files('libs/classes.jar')
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.browser:browser:1.4.0'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
}

afterEvaluate {
    publishing {
        publications {
            // Creates two publications with different artifactIds
            core(MavenPublication) {
                from components.coreRelease
                artifactId = 'core'
            }
            unity(MavenPublication) {
                from components.unityRelease
                artifactId = 'unity'
            }
        }
    }
}