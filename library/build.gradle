plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def keystore = new Properties()
keystore.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    namespace 'ai.bitlabs.sdk'

    compileSdk 34 // NOTE: For Jitpack Compatibility

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        multiDexEnabled true

        buildConfigField 'String', 'APP_TOKEN', keystore.getProperty('appToken', '"APP_TOKEN"')

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions = ['sdk']

    productFlavors {
        core {}
        unity {}
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    unityCompileOnly files('libs/classes.jar')

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation 'com.google.zxing:core:3.5.2'
    implementation 'com.caverock:androidsvg-aar:1.4'

    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.1'
    testImplementation 'com.google.truth:truth:1.1.4'

    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'io.mockk:mockk-android:1.13.1'
    androidTestImplementation 'androidx.test.ext:truth:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications { // Creates two publications with different artifactIds
            core(MavenPublication) {
                from components.coreRelease
                artifactId = 'core'
            }
            unity(MavenPublication) {
                from components.unityRelease
                artifactId = 'unity'
            }
        }
    }
}