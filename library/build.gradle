plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    namespace 'ai.bitlabs.sdk'

    compileSdkVersion 31 // NOTE: For Jitpack Compatibility

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 31
    }

    flavorDimensions = ['sdk']

    productFlavors {
        core {}
        unity {}
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
}

dependencies {
    unityCompileOnly files('libs/classes.jar')

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    implementation 'com.caverock:androidsvg-aar:1.4'

    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
}

afterEvaluate {
    publishing {
        publications { // Creates two publications with different artifactIds
            core(MavenPublication) {
                from components.coreRelease
                artifactId = 'core'
            }
            unity(MavenPublication) {
                from components.unityRelease
                artifactId = 'unity'
            }
        }
    }
}